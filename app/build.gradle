plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

apply from: 'dependencies.gradle'
def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretsProperties = new Properties()
secretsProperties.load(new FileInputStream(secretsPropertiesFile))

android {
    compileSdk versions.compileSdk

    defaultConfig {
        applicationId 'com.cornellappdev.scoop'
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField 'String', 'PLACES_API_KEY', secretsProperties['PLACES_API_KEY']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion versions.compose
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:29.3.1')

    implementation composeDependencies.values()
    implementation accompanistDependencies.values()
    implementation firebaseDependencies.values()
    implementation androidxDependencies.values()
    implementation jetbrainsDependencies.values()
    implementation apiDependencies.values()
}
